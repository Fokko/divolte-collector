apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'com.commercehub.gradle.plugin.avro'
apply plugin: 'js'

sourceCompatibility = 1.8
version = '0.1-SNAPSHOT'
mainClassName = 'io.divolte.server.Server'

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath group: 'com.commercehub.gradle.plugin', name: 'gradle-avro-plugin', version: '0.2.0'
        classpath group: 'com.eriwen', name: 'gradle-js-plugin', version: '1.12.1'
    }
}

dependencies {
    compile group: 'io.undertow', name: 'undertow-core', version: '1.0.15.Final'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
    compile group: 'com.typesafe', name: 'config', version: '1.2.1'
    compile group: 'com.google.guava', name: 'guava', version: '17.0'
    compile group: 'org.apache.avro', name: 'avro', version: '1.7.7'

    runtime group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
}

sourceSets {
    main {
        output.dir("${buildDir}/generated-resources/javascript", builtBy: 'minifyJs')
    }
}

javascript.source {
    dev {
        js {
            srcDir 'src/main/javascript'
            include 'dvt.js'
        }
    }
}

minifyJs {
    source = javascript.source.dev.js.files
    dest = file("${buildDir}/generated-resources/javascript/static/dvt.js")
    sourceMap = file("${buildDir}/generated-resources/javascript/static/dvt.sourcemap.json")
    closure {
        warningLevel = 'QUIET'
        compilationLevel = 'ADVANCED_OPTIMIZATIONS'
    }
}
